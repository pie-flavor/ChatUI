buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

version = "1.0.0"

group = "com.simon816.chatui"

archivesBaseName = "ChatUI"

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    // For launcherwrapper needed by mixin
    maven {
        name = 'mojang'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'bstats'
        url = 'http://repo.bstats.org/content/repositories/releases/'
    }
}

compileJava.options.encoding = 'UTF-8'
// Stop AP generating a mcmod.info file
compileJava.options.compilerArgs += '-proc:none'

configurations {
    fastutil
    shaded
    compile.extendsFrom shaded
}

dependencies {
    compile 'org.spongepowered:spongeapi:5.0.0'
    fastutil 'it.unimi.dsi:fastutil:7.0.12' // from spongecommon
    compile 'org.spongepowered:spongecommon:5.0.0-SNAPSHOT:dev'
    shaded 'org.bstats:bstats-sponge-lite:1.1'
}

processResources {
    from('src/main/resources') {
        include 'mcmod.info'
        expand(version: project.version, apiVersionRange: '[5.0.0,6.0.0)')
    }
}

jar {
    classifier = 'dev'
}

shadowJar {
    from(zipTree(project.configurations.fastutil.first())) {
        include 'it/unimi/dsi/fastutil/chars/*Char2ObjectFunction.class'
        include 'it/unimi/dsi/fastutil/chars/*Char2ObjectFunction$*'
        include 'it/unimi/dsi/fastutil/chars/*Char2ObjectMap.class'
        include 'it/unimi/dsi/fastutil/chars/*Char2ObjectMap$*'
        include 'it/unimi/dsi/fastutil/chars/*Char2ObjectOpenHashMap*'
        include 'it/unimi/dsi/fastutil/chars/*CharSet.class'
        include 'it/unimi/dsi/fastutil/chars/*CharSet$*'
        include 'it/unimi/dsi/fastutil/chars/*CharCollection.class'
        include 'it/unimi/dsi/fastutil/chars/*CharCollection$*'
        include 'it/unimi/dsi/fastutil/chars/*CharIterable.class'
        include 'it/unimi/dsi/fastutil/chars/*CharIterable$*'

        include 'it/unimi/dsi/fastutil/ints/*Int2CharFunction.class'
        include 'it/unimi/dsi/fastutil/ints/*Int2CharFunction$*'
        include 'it/unimi/dsi/fastutil/ints/*Int2CharMap.class'
        include 'it/unimi/dsi/fastutil/ints/*Int2CharMap$*'
        include 'it/unimi/dsi/fastutil/ints/*Int2CharOpenHashMap*'
    }
    configurations = [project.configurations.shaded]
    classifier = ''
    relocate 'org.bstats', 'com.simon816.chatui.bstats'
}

assemble.dependsOn = [shadowJar]
